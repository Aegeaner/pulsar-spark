/**
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id "com.github.hierynomus.license" version "0.14.0"
}

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

allprojects {
    apply plugin: 'idea'
    if (file("src/main/java").isDirectory()) {
        apply plugin: 'java'
    }

    // Plugin Configurations
    apply from: "$rootDir/gradle/checkstyle.gradle"
    apply from: "$rootDir/gradle/findbugs.gradle"
    apply from: "$rootDir/gradle/idea.gradle"
    apply from: "$rootDir/gradle/java.gradle"
    apply from: "$rootDir/gradle/license.gradle"
    
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    version = segmentxVersion
    group = "org.apache.bookkeeper.segmentx"

    configurations.all {
        resolutionStrategy {
            force "org.slf4j:slf4j-api:" + slf4jVersion
        }
    }
}

project('test:common') {
    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
    }
}

project('segment') {
    dependencies {
        testCompile project(':test:common')
        compile group: 'org.apache.bookkeeper', name: 'bookkeeper-server', version: bookkeeperVersion
        compile group: 'org.apache.pulsar', name: 'managed-ledger', version: pulsarVersion
        compile group: 'org.apache.pulsar', name: 'pulsar-client-admin', version: pulsarVersion
    }
}

project('connectors:flink') {
    dependencies {
        compile project(':segment')
        compile group: 'org.apache.flink', name: 'flink-streaming-java_' + scalaVersion, version: flinkVersion
        compile group: 'org.apache.flink', name: 'flink-table_' + scalaVersion, version: flinkVersion
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.8.1'
}

// license plugin

task licenseFormatAll(type: com.hierynomus.gradle.license.tasks.LicenseFormat) {
    source = fileTree(dir: ".").include("**/*")
}
licenseFormat.dependsOn licenseFormatAll

task licenseCheckAll(type: com.hierynomus.gradle.license.tasks.LicenseCheck) {
    source = fileTree(dir: ".").include("**/*")
}
